// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
	// zero x input if desired
	And16(a=x, b=false, out=zeroedX);
	Mux16(a=x, b=zeroedX, sel=zx, out=newX);
	
	// negate x input if desired
	Not16(in=newX, out=notX);
	Mux16(a=newX, b=notX, sel=nx, out=finalX);
	
	// zero y input if desired
	And16(a=y, b=false, out=zeroedY);
	Mux16(a=y, b=zeroedY, sel=zy, out=newY);
	
	// negate y input if desired
	Not16(in=newY, out=notY);
	Mux16(a=newY, b=notY, sel=ny, out=finalY);
	
	// compute the value for f
	Add16(a=finalX, b=finalY, out=addXY);
	And16(a=finalX, b=finalY, out=andXY);
	Mux16(a=andXY, b=addXY, sel=f, out=fResult);
	
	// negate output if desired
	Not16(in=fResult, out=negatedF);
	Mux16(a=fResult, b=negatedF, sel=no, out=finalOut);
	
	// set flag if out==0
	And16(a=finalOut, b=true, out[0..7]=finalOutFirstHalf, out[8..15]=finalOutSecondHalf);
	Or8Way(in=finalOutFirstHalf, out=firstOr);
	Or8Way(in=finalOutSecondHalf, out=secondOr);
	Or(a=firstOr, b=secondOr, out=thirdOr);
	Not(in=thirdOr, out=zr);
	
	// set flag if output is < 0
	And16(a=finalOut, b=true, out[15]=ng);
	
	// Do nothing, just need to get it to an output gate
	Not16(in=finalOut, out=tempOut);
	Not16(in=tempOut, out=out);
	
	
	
	
	
	
	
	
	
	
	
}
